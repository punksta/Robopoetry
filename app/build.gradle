apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

def baseVersionCode = 10
def baseVersionName = "1.4"

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.1'

    defaultConfig {
        applicationId "com.punksta.apps.robopoetry"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode baseVersionCode
        versionName baseVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        kotlinOptions.jvmTarget = "1.6"

        buildConfigField 'String', 'yandexApiKey', "\"37064543-1d5c-4a88-80f6-d84ed607d874\""
    }

    lintOptions {
        disable 'MissingTranslation'
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    dexOptions {
        javaMaxHeapSize "2g"
    }

    flavorDimensions "minApi"

    productFlavors {

        minApi17 {
            vectorDrawables.useSupportLibrary = true
            dimension "minApi"
            minSdkVersion 17
            versionCode 10 * baseVersionCode + 1
        }

        minApi21 {
            dimension "minApi"
            minSdkVersion 21
            versionCode 10 * baseVersionCode + 2
        }

        minApi26 {
            dimension "minApi"
            minSdkVersion 26
            versionCode = 10 * baseVersionCode + 3
        }
    }
}


android.applicationVariants.all { variant ->
    version = "${baseVersionName}-${variant.flavorName}-${variant.buildType.name}"
    variant.mergedFlavor.versionName =  version
    variant.resValue "string", "versionName", version
    variant.resValue "string", "versionCode", variant.versionCode.toString()
}


configurations {
    all {
        exclude module: 'commons-logging'
    }
}

android.compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_6
    targetCompatibility JavaVersion.VERSION_1_6
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:support-compat:27.0.1'
    implementation 'com.android.support:appcompat-v7:27.0.1'
    implementation 'com.android.support:support-v4:27.0.1'
    implementation 'com.android.support:recyclerview-v7:27.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.6'
    implementation 'com.opencsv:opencsv:4.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.2'

    minApi26Implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    minApi21Implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    minApi17Implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'com.yandex.android:speechkit:2.5.0'
    implementation 'nz.bradcampbell:paperparcel:2.0.4'
    implementation 'nz.bradcampbell:paperparcel-kotlin:2.0.4'

    implementation 'com.daimajia.easing:library:2.0@aar'
    implementation 'com.daimajia.androidanimations:library:2.3@aar'

    // Optional
    kapt 'nz.bradcampbell:paperparcel-compiler:2.0.4'
    implementation 'com.android.support:multidex:1.0.2'

    implementation 'org.funktionale:funktionale:0.8'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'
}